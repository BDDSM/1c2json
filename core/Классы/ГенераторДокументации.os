#Использовать ".\internal"

Перем Лог;
Перем КаталогИсходников;
Перем АнализаторМодулей1С;
Перем ГенераторРезультата;

Процедура ПриСозданииОбъекта()

	Лог = Константы1c2json.ПолучитьЛог();

КонецПроцедуры

Процедура ОбработатьФайлы(Знач Настройки) Экспорт

	КаталогИсходников = Настройки.КаталогИсходников;

	Если Настройки.Свойство("КаталогИсходников", КаталогИсходников)
	   И НЕ ПустаяСтрока(КаталогИсходников) Тогда
	   Лог.Информация("Каталог исходников: %1", КаталогИсходников);
	Иначе
		Лог.Ошибка("Не задан каталог исходников. Обработка не выполнена.");
		Возврат;
	КонецЕсли;

	АнализаторМодулей1С    = Новый АнализаторМодулей1С(Настройки);
	ГенераторРезультата    = Новый ГенераторФорматаJSON(Настройки);

	Если Настройки.Свойство("ФильтрФайлов") Тогда
		ФильтрФайлов = Настройки.ФильтрФайлов;
	Иначе
		ФильтрФайлов = Неопределено;
	КонецЕсли;

	// Подготовка необходимых данных для генерации файлов форматов
	ГенераторРезультата.ПередОбработкой();

	ОбработаноФайлов = 0;

	Если ФильтрФайлов = Неопределено Тогда
		
		НайденныеФайлы = НайтиФайлы(КаталогИсходников, "*.bsl", Истина);
		Лог.Информация("Найдено %1 bsl-файлов", НайденныеФайлы.Количество());
		Для Каждого ФайлОбъект Из НайденныеФайлы Цикл
			ОбработатьФайл(ФайлОбъект.ПолноеИмя, ФайлОбъект.Расширение);
			ОбработаноФайлов = ОбработаноФайлов + 1;
		КонецЦикла;
		
	Иначе
		Для Каждого ПолноеИмяФайла Из ФильтрФайлов Цикл
			Если ТипЗнч(ПолноеИмяФайла) = Тип("Файл") Тогда
				ФайлОбъект = ПолноеИмяФайла;
			Иначе
				ФайлОбъект = Новый Файл(ПолноеИмяФайла);
			КонецЕсли;
			ОбработатьФайл(ПолноеИмяФайла, ФайлОбъект.Расширение);
			ОбработаноФайлов = ОбработаноФайлов + 1;
		КонецЦикла;
	КонецЕсли;

	// Окончание генерации файлов формата документации. Генерация оглавлений документации.
	ГенераторРезультата.ПослеОбработки();

	Лог.Информация("Обработано bsl-файлов: " + ОбработаноФайлов);

КонецПроцедуры

Процедура ОбработатьФайл(Знач ПолноеИмяФайла, Знач РасширениеФайлаСТочкой)

	//Сообщить("Обработка файла " + ПолноеИмяФайла);

	ИмяФайла = СтрЗаменить(ПолноеИмяФайла, КаталогИсходников, "");
	Если Лев(ИмяФайла, 1) = ПолучитьРазделительПути() Тогда
		ИмяФайла = Сред(ИмяФайла, 2);
	КонецЕсли;

	ИмяОбъектаМетаданных = ОбработкаМетаданных1С.ОбъектМетаданныхПоИмениФайла(ИмяФайла, РасширениеФайлаСТочкой);

	ОписаниеФайла = Новый Структура;
	ОписаниеФайла.Вставить("ПолноеИмяФайла", ПолноеИмяФайла);
	ОписаниеФайла.Вставить("ИмяФайла", ИмяФайла);
	ОписаниеФайла.Вставить("ОбъектМетаданных", ИмяОбъектаМетаданных);

	ОписаниеМодуля = АнализаторМодулей1С.МодульИзФайла(ПолноеИмяФайла);
	Если ОписаниеМодуля.Количество() > 0 Тогда
		ОписаниеФайла.Вставить("Тип", "Module");
		ОписаниеФайла.Вставить("Методы", ОписаниеМодуля.Методы);
		ГенераторРезультата.ОбработатьФайл(ОписаниеФайла);
	КонецЕсли;

КонецПроцедуры